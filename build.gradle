plugins {
  id 'java'
  id 'maven'
  id 'signing'
  id 'net.minecrell.licenser' version '0.4.1'
}

group 'com.ichorpowered'
version '1.0.0-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

license {
  header = file("header.txt")
  newLine = false
  ext {
    name = "Indigo"
    organization = "IchorPowered"
    url = "http://ichorpowered.com"
  }

  include("**/*.java", "**/*.groovy", "**/*.kt")
}

repositories {
  mavenLocal()
  mavenCentral()
}

dependencies {
  compile 'com.google.code.findbugs:jsr305:3.0.2'
  compile 'com.google.code.gson:gson:2.8.0'
}

task javadocJar(type: Jar) {
  classifier 'javadoc'
  from javadoc
}

task sourcesJar(type: Jar) {
  classifier 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives javadocJar
  archives sourcesJar
}

signing {
  required { !project.version.endsWith('-SNAPSHOT') && gradle.taskGraph.hasTask(':uploadArchives') && project.hasProperty('signing.keyId') }
  sign configurations.archives
}

jar {
  manifest.attributes(
    'Automatic-Module-Name': 'com.ichorpowered.indigo'
  )
}

if(project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword')) {
  uploadArchives {
    enabled = System.getenv('TRAVIS') == null || project.version.endsWith('-SNAPSHOT')

    repositories {
      mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
          authentication(userName: sonatypeUsername, password: sonatypePassword)
        }

        snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
          authentication(userName: sonatypeUsername, password: sonatypePassword)
        }

        pom.project {
          description 'An error report generator'
          name project.name
          url 'https://github.com/IchorPowered/indigo/'

          issueManagement {
            system 'GitHub Issues'
            url 'https://github.com/IchorPowered/indigo/issues'
          }

          licenses {
            license {
              name 'MIT License'
              url 'https://opensource.org/licenses/MIT'
            }
          }

          scm {
            connection 'scm:git@github.com:IchorPowered/indigo.git'
            developerConnection 'scm:git@github.com:IchorPowered/indigo.git'
            url 'https://github.com/IchorPowered/indigo/'
          }
        }
      }
    }
  }
}
